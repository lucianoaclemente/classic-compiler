program: "program"i identifier ";" (compiler_defined_directives)* block "."

identifier: /\b[a-zA-Z]([a-zA-Z0-9\_]{0,30})\b/

block: declaration_part statement_part

declaration_part: label_declaration_part? constant_definition_part? type_definition_part? variable_declaration_part? procedure_and_function_declaration_part?

label_declaration_part: "label"i label_identifier ("," label_identifier)* ";"

label_identifier: identifier

constant_definition_part: "const"i constant_definition (";" constant_definition)* ";" 

constant_definition: identifier "=" constant

constant: primitive | constant_identifier | sign constant_identifier

sign: "+" | "-"

constant_identifier: identifier

type_definition_part: "type"i type_definition (";" type_definition )* ";"

type_definition: identifier "=" type

type: simple_type | structured_type | pointer_type

simple_type: scalar_type | subrange_type | type_identifier

scalar_type: "(" identifier ("," identifier)* ")"

subrange_type: constant ".." constant

type_identifier: identifier

structured_type: array_type | record_type

array_type: "array"i "[" index_type ("," index_type)* "]" "of"i component_type

index_type: simple_type
 
component_type: type

record_type: "record"i field_list "end"i

field_list: fixed_part 

fixed_part: record_section (";" record_section)* ";"

record_section: field_identifier ("," field_identifier)* ":" type

pointer_type: "^" type_identifier

variable_declaration_part: "var"i variable_declaration (";" variable_declaration)* ";"

variable_declaration: identifier ("," identifier)* ":" type

procedure_and_function_declaration_part: procedure_or_function_declaration (";" procedure_or_function_declaration)* ";"

procedure_or_function_declaration: procedure_declaration | function_declaration

procedure_declaration: procedure_heading block

procedure_heading: "procedure"i identifier ";" | "procedure"i identifier "(" formal_parameter_section (";" formal_parameter_section)* ")" ";"

formal_parameter_section: parameter_group | "var"i parameter_group

parameter_group: identifier ("," identifier)* ":" type_identifier

function_declaration: function_heading block

function_heading: "function"i identifier ":" result_type ";" | "function"i identifier "(" formal_parameter_section (";" formal_parameter_section)* ")" ":" result_type ";"

result_type: type_identifier

statement_part: compound_statement

compound_statement: "begin"i statement_sequence? "end"i 

statement_sequence: statement (";" statement)* ";"

statement: unlabelled_statement | label_identifier ":" unlabelled_statement

unlabelled_statement: simple_statement | structured_statement

simple_statement: assignment_statement | procedure_statement | go_to_statement

assignment_statement: variable ":=" expression 

variable: entire_variable | component_variable | referenced_variable

entire_variable: variable_identifier

variable_identifier: identifier

component_variable: field_designator

field_designator: record_variable "." field_identifier

record_variable: variable

field_identifier: identifier

referenced_variable: pointer_variable "^"

pointer_variable: variable

expression: simple_expression | simple_expression relational_operator simple_expression

relational_operator: "=" | "<>" | "<" | "<=" | ">=" | ">" | "in"i

simple_expression: term | sign term | simple_expression adding_operator term

adding_operator: "+" | "-" | "or"i

term: factor | term multiplying_operator factor

multiplying_operator: "*" | "/" | "div"i | "mod"i | "and"i

factor: variable | unsigned_constant | "(" expression ")" | function_designator | "not"i factor

unsigned_constant: primitive | constant_identifier

function_designator: function_identifier | function_identifier "(" actual_parameter ("," actual_parameter )* ")"

function_identifier: identifier

procedure_statement: procedure_identifier | procedure_identifier "(" actual_parameter ("," actual_parameter )* ")"

procedure_identifier: identifier

actual_parameter: expression | variable | procedure_identifier | function_identifier

go_to_statement: "goto"i label_identifier

structured_statement: compound_statement | conditional_statement | repetitive_statement | with_statement

conditional_statement: if_statement | case_statement

if_statement: "if"i expression "then"i statement | "if"i expression "then"i statement "else"i statement

case_statement: "case"i expression "of"i case_list_element (";" case_list_element )* ";" "end"i

case_list_element: case_label_list ":" statement

case_label_list: case_label ("," case_label )*

case_label: constant

repetitive_statement: while_statement | repeat_statement | for_statement

while_statement: "while"i expression "do"i statement

repeat_statement: "repeat"i statement_sequence "until"i expression

for_statement: "for"i control_variable ":=" for_list "do"i statement

control_variable: identifier

for_list: initial_value "to"i final_value | initial_value "downto"i final_value

initial_value: expression

final_value: expression

with_statement: "with"i record_variable_list "do"i statement

record_variable_list: record_variable ("," record_variable)*

// ================================================================
// LOW LEVEL DEFINITIONS
// ================================================================

compiler_defined_directives: include_directive

include_directive: "INCLUDE"i "'" filename "'" ";"

filename: /[\w,\s-]+\.[A-Za-z]{3}/

primitive: INTEGER
		| FLOAT
		| BOOLEAN
		| CHAR
		| STRING
		| HEXLITERAL
		| BINLITERAL

// ---------------------- Primitives 	   
	      
INTEGER: /([+-]?[1-9]\d*|0)/
FLOAT: /[-+]?\-?\d+\.\d+/
BOOLEAN: /(?i)(true|false)/
CHAR: "'" /([^'])/ "'"
STRING: "'" /([^']+){2,}/ "'"

// ---------------------- Literal

HEXLITERAL: /[$][0-9a-fA-F]+/	
BINLITERAL: /[%][01]+/   

COMMENT:  /\{(.|\n)+\}/
       |  /\/\/(.)+/

	%import common.WS
	%import common.LETTER
	%import common.DIGIT
	%ignore COMMENT
    %ignore WS